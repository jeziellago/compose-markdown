plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.2.0'
}

android {
    compileSdk 36
    namespace 'dev.jeziellago.compose.markdown'

    defaultConfig {
        minSdk 26
        targetSdk 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        compose true
    }
    kotlin {
        jvmToolchain(21)
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}

dependencies {
    // Compose BOM (Bill of Materials) - matches your app
    implementation platform("androidx.compose:compose-bom:2025.07.00")
    
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'

    final def markwon_version = '4.6.2'

    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation "io.noties.markwon:html:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"
    implementation "io.noties.markwon:ext-tasklist:$markwon_version"

    // TODO: Change it after migrating markwon project
    implementation "com.github.jeziellago:Markwon:58aa5aba6a"

    api("io.coil-kt:coil:2.7.0")
    api("io.coil-kt:coil-gif:2.7.0")

    // Jetpack Compose - versions from BOM
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'dev.jeziellago'
                artifactId = 'compose-markdown'
                version = '1.0.0-local'
            }
        }
    }
}
